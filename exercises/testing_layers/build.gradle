plugins {
    id 'java'
    id 'application'
    id 'jvm-test-suite'
}

group = 'com.skillerwhale'
version = '1.0.0'
description = 'Testing at different layers exercise'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.1'
    playwrightVersion = '1.53.0'
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
                implementation "org.assertj:assertj-core:3.24.2"
                implementation "com.microsoft.playwright:playwright:${playwrightVersion}"
            }

            targets {
                all {
                    testTask.configure {
                        testLogging {
                            events "passed", "skipped", "failed"
                            exceptionFormat = 'full'
                            showStandardStreams = true
                            showExceptions = true
                            showCauses = true
                            showStackTraces = true

                            afterSuite { desc, result ->
                                if (!desc.parent) {
                                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                                    def startItem = '|  ', endItem = '  |'
                                    def repeatLength = startItem.length() + output.length() + endItem.length()
                                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task playwright(type: JavaExec) {
    classpath = testing.suites.test.sources.runtimeClasspath
    mainClass = 'com.microsoft.playwright.CLI'
}

application {
    mainClass = 'com.skillerwhale.wordle.WordleApplication'
}
